apply plugin: 'java'
apply plugin: 'maven'

apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jdepend'
apply plugin: "jacoco"
apply plugin: 'project-report'
apply plugin: 'build-dashboard'

apply plugin: 'idea'
apply plugin: 'eclipse'

ext {
    jmockVersion = "2.6.0"
    slf4jVersion = "1.6.1"
}

group = 'kyototycoon'
archivesBaseName = 'kyototycoon'
version = '0.7-SNAPSHOT'

sourceCompatibility = 1.5

configurations {
    deployerJars
}

repositories {
    mavenCentral()
    maven {
        url "http://maven.twttr.com"
    }
}

dependencies {
    compile "commons-codec:commons-codec:1.5"
    compile "org.slf4j:slf4j-api:" + slf4jVersion
    compile "com.twitter:finagle-http:5.3.22"

    testCompile "junit:junit:4.11"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "org.jmock:jmock:$jmockVersion"
    testCompile "org.jmock:jmock-legacy:$jmockVersion"
    testCompile "org.jmock:jmock-junit4:$jmockVersion"
    testRuntime "org.slf4j:slf4j-log4j12:$slf4jVersion"

    deployerJars "org.apache.maven.wagon:wagon-webdav:1.0-beta-2"
}

findbugs {
    ignoreFailures = true
}

pmd {
    ignoreFailures = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
     classifier = 'sources'
     from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
     classifier = 'javadoc'
     from javadoc.destinationDir
}

artifacts {
     archives sourcesJar
     archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "dav:https://zact.googlecode.com/svn/maven/repository") {
                authentication(userName: mavenRepoUser, password: mavenRepoPassword)
            }
            snapshotRepository(url: "dav:https://zact.googlecode.com/svn/maven/snapshot-repository") {
                authentication(userName: mavenRepoUser, password: mavenRepoPassword)
            }
            pom.project {
                name 'Kyoto Tycoon Java Client'
                description 'This module implements the pure Java client which connects to the server of Kyoto Tycoon and speaks its TSV-RPC protocol.'
                url 'https://github.com/eungju/kyototycoon-java'

                scm {
                    connection 'scm:git:https://github.com/eungju/kyototycoon-java.git'
                    developerConnection 'scm:git:ssh://git@github.com:eungju/kyototycoon-java.git'
                    url 'https://github.com/eungju/kyototycoon-java'
                }

                issueManagement {
                    url 'https://github.com/eungju/kyototycoon-java/issues'
                }

                licenses {
                    license {
                        name 'Apache License 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id 'eungju'
                        name 'Park Eung-ju'
                    }
                }
            }
        }
    }
}
